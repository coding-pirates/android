buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
    }
}

apply plugin: 'com.android.application'

repositories {
    google()
    jcenter()
    maven {
        name = "Coding Pirates Logic"
        url = "https://maven.pkg.github.com/coding-pirates/logic"
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_PACKAGE_READ_TOKEN")
        }
    }
    maven {
        name = "Coding Pirates Network"
        url = "https://maven.pkg.github.com/coding-pirates/network"
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_PACKAGE_READ_TOKEN")
        }
    }
    maven {
        name = "Coding Pirates Client"
        url = "https://maven.pkg.github.com/coding-pirates/client"
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_PACKAGE_READ_TOKEN")
        }
    }
}

android {
    dataBinding.enabled = true
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "de.upb.codingpirates.battleships.android"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName = project.version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions.annotationProcessorOptions.includeCompileClasspath = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        pickFirst  'META-INF/**'
        exclude '**/log4j2.xml'
    }
}

dependencies {
    //project intern dependencies
    implementation(group:  'de.upb.codingpirates.battleships', name: 'network', version: project.network_version){
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
        exclude group: 'com.google.inject', module: 'guice'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
    }
    implementation(group: 'de.upb.codingpirates.battleships', name: 'logic', version: project.logic_version) {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
    }
    implementation(group: 'de.upb.codingpirates.battleships', name: 'client', version: project.client_version){
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.google.inject', module: 'guice'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
    }

    implementation group: 'io.reactivex.rxjava2', name: 'rxandroid', version: '2.1.1'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.0-alpha1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: '2.3'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0-alpha1'
    implementation group: 'com.google.guava', name: 'guava', version: '28.1-android'
    implementation group: 'org.roboguice', name: 'roboguice', version: '4.0.0'

    //android dependencies
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.navigation:navigation-fragment:2.2.0"
    implementation "androidx.navigation:navigation-ui:2.2.0"
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "com.google.android.material:material:1.2.0-alpha04"

    //test dependencies
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'

}

